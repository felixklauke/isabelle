plugins {
    id 'org.sonarqube' version "2.8"
    id 'java'
}

group 'de.d3adspace.isabelle'
version '2.0.0'

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {

        mavenLocal()
        mavenCentral()
    }

    dependencies {

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.5.2'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'
        testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.5.2'
        testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.5.15'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    }

    test {

        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                from components.java
            }
        }
        repositories {
            maven {
                credentials {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }

                def releasesRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-releases/'
                def snapshotsRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.setDestination(new File("${buildDir}/reports/jacoco/report.xml"))
        html.setEnabled(true)
        html.setDestination(new File("${buildDir}/reports/jacoco/report.html"))
    }

    dependencies {
        subprojects {
            test
        }
    }
}
